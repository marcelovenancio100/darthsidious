plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
    id "org.springframework.cloud.contract" version "2.1.3.RELEASE" apply false
}

allprojects {
    group = 'me.fwfurtado.neartaxi'
    version = '0.0.2-SNAPSHOT'

    repositories {
        maven {
            url 'http://localhost:8081/nexus/repository/maven-public/'
            credentials {
                username 'fetcher'
                password 'fetcher123'
            }
        }
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    ext {
        set('springCloudVersion', "Greenwich.SR3")
        set('springBootAdminVersion', "2.1.5")
        set('releaseRepository', "http://localhost:8081/nexus/repository/maven-releases/")
        set('snapshotRepository', "http://localhost:8081/nexus/repository/maven-snapshots/")
    }

}

def ignoredProjects = ["services", "infra"]

configure(subprojects.findAll { !ignoredProjects.contains(it.name) }) {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        sourceCompatibility = '11'
        targetCompatibility = '11'
        options.compilerArgs += '-parameters'
        options.compilerArgs += '-Xlint:unchecked'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test')
    }

    def nexusUrl = project.hasProperty('release') ? ext['releaseRepository'] : ext['snapshotRepository']

    publishing {
        repositories {
            maven {
                url nexusUrl

                credentials {
                    username 'ci-user'
                    password 'ci123'
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = project.group
                version = project.version

                afterEvaluate {
                    artifactId project.name
                }

                from components.java
            }
        }
    }
}
